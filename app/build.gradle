apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.maxleapmobile.gitmaster"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 2
        versionName "1.1"
    }

    signingConfigs {

        release {

        }
    }

    File propFile = file('signing.properties')
    if (propFile.exists()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            android.buildTypes.release.signingConfig = null
        }
    } else {
        android.buildTypes.release.signingConfig = null
    }

    buildTypes {
        release {
            buildConfigField("boolean", "LOG_DEBUG", "false")
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            buildConfigField("boolean", "LOG_DEBUG", "true")
        }

        applicationVariants.all { variant ->
            def apk = variant.outputs[0].outputFile;
            def versionName = android.defaultConfig.versionName;
            def newName = apk.name.replace(".apk", "-v" + versionName + ".apk");
            newName = newName.replace("app", "maxleapgit");
            variant.outputs[0].outputFile = new File(apk.parentFile, newName);
        }
    }

    productFlavors {
        google {
            applicationId 'com.maxleapmobile.gitmaster'
        }
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true
    }
}

ext {
    supportLibVersion = '23.1.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
}
